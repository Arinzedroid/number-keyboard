apply plugin: 'com.android.library'

ext.versionMajor = 1 // API Changes, adding big new feature, redesign the App
ext.versionMinor = 1 // New features in a backwards-compatible manner
ext.versionPatch = 0 // Backwards-compatible bug fixes
ext.versionClassifier = null // Pre-releases (alpha, beta, rc, SNAPSHOT...)

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode generateVersionCode()
        versionName generateVersionName()
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    api "com.android.support:gridlayout-v7:27.1.1"
}

/**
 * Build number (currently not available in JitPack).
 */
private static Integer getBuildNumber() {
    return System.getenv()["buildNumber"] as Integer ?: 0
}

/**
 * Version code: MMMmmppbbb
 */
private Integer generateVersionCode() {
    Integer versionCode = ext.versionMajor * 10000000 +
            ext.versionMinor * 100000 +
            ext.versionPatch * 1000 +
            (getBuildNumber() % 1000)
    println "Version code: " + versionCode
    return versionCode
}

/**
 * Version name: <major>.<minor>.<patch>
 */
private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    println "Version name: " + versionName
    return versionName
}
